Tutorial 2 | January 23^rd^, 2019

# Problem 1: Factorial

Write a MIPS program that takes non-negative integer $n$ in `$1` and stores $n!$ in `$3`

```ruby
factorial:
	sw $31, -4($30) ; store the important start vals
	sw $1, -8($30)
	sw $11, -12($30)
	lis $31
	.word 12
	sub $30, $30, $31
	lis $11
	.word 1
	bne $1, $0, recurse
	add $3, $11, $0
	beq $0, $0, clean
recurse:
	sub $1, $1, $11
	lis $31
	.word factorial
	jalr $31 		; puts PC in $31 and jumps to whatever was in $31 before
	add $1, $1, $11
	mult $3, $1
	mflo $3
clean:
	lis $31
	.word 12
	add $30, $30, $31
	lw $11, -12($30)
	lw $1, -8($30)
	lw $31, -4($30)
	jr $31
```

# Problem 6: Using the stack in MIPS

Write a MIPS program which reads in characters from `stdin` until EOF is encountered, then prints the same characters out backwards to `stdout`. Use the stack to store the characters.

```ruby
# First, we need to load the constants
	lis $27
	.word 0xffff0004 ; stdin
	lis $28
	.word 0xffff000c ; stdout
	lis $4
	.word 4 ; used to shift stack pointer
	lis $24
	.word -1 ; EOF
	add $26, $30, $0 ; stores where our stack pointer started
reader:
	lw $3, 0($27) ; reads a character
	beq $3, $24, printer
	sw $3, -4($30)
	sub $30, $30, $4
	beq $0, $0, reader
printer:
	beq $26, $30, end
	add $30, $30, $4
	lw $3, -4($30)
	sw $3, 0($28)
	beq $0, $0, printer
end:
	jr $31
```

# Problem 2: Fibonacci

Write a MIPS program which takes a non-negative integer $n$ in `$1` and stores $f_n$ in `$3`.

```ruby
# Load in our constants and initial setup
	lis $11
	.word 1
	add $3, $0, $0	; holding f(i)
	add $4, $11, $0 ; holding f(i+1)
	sw $1, -4($30)
	lis $24
	.word 4
	sub $30, $30, $24
fib:
	beq $1, $0, end
	add $5, $4, $0 ; save f(i+1)
	add $4, $3, $4 ; load next fi
	add $3, $5, $0 ; gets f(i+1)
	sub $1, $1, $11
	beq $0, $0, fib
end:
	jr $31
```







